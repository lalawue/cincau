--
-- Copyright (c) 2020 lalawue
--
-- This library is free software; you can redistribute it and/or modify it
-- under the terms of the MIT license. See LICENSE for details.
--

-- session was server in memory kv store, can keep any thing
--

import Cookie from "session.cookie_core"
import UUIDCore from "session.uuid_core"
import List from "linked-list"

-- update time struct, push to last
fn _updateTime(lst, tm) {
    tm.time = os.time()
    lst:remove(tm)
    lst:pushl(tm)
}

struct Session {
    _sessions = {}
    _lst = List.new()

    -- check session exist, from req
    static fn inSession(req, skey) {
        if not req or not skey {
            return false
        }
        uuid = req.cookies[skey]
        sinfo = Self._sessions[uuid]
        if sinfo and sinfo.tm {
            _updateTime(Self._lst, sinfo.tm)
        }
        return (sinfo and true) or false
    }

    --[[ store session info to response using 'Set-Cookie'
    local options = [
        max_age = 3600,
        domain = ".example.com",
        path = "/",
        expires = time,
        http_only = true,
        secure = true
    ]
    local expected =
        "foo=bar; Max-Age=3600; Domain=.example.com; " ..
        "Path=/; Expires=Wed, 23 Apr 2014 13:01:14 GMT; " ..
        "HttpOnly; Secure
    ]]
    static fn createSession(req, response, skey, options) {
        if not response or Self.inSession(req, skey) {
            return false
        }
        -- set cookie to respoinse
        uuid = UUIDCore.new()
        cookie_str = Cookie.build({[skey] : uuid}, options)
        response:setHeader("Set-Cookie", cookie_str)
        -- set cookie to req
        req.cookies[skey] = uuid
        -- create session table
        now = os.time()
        tm = {
            time : now,
            uuid : uuid
        }
        Self._sessions[uuid] = { tm : tm }
        Self._lst:pushl(tm)
        return true
    }

    -- check in session first, get hval from session with hkey
    static fn getValue(req, skey, hkey) {
        if not req or not skey or not hkey {
            return nil
        }
        uuid = req.cookies[skey]
        sinfo = Self._sessions[uuid]
        if not uuid or not sinfo {
            return nil
        }
        _updateTime(Self._lst, sinfo.tm)
        return sinfo[hkey]
    }

    -- check in session first, set hkey, hval
    static fn setValue(req, skey, hkey, hval) {
        if not req or not skey {
            return false
        }
        if not hkey or not hval {
            return false
        }
        uuid = req.cookies[skey]
        sinfo = Self._sessions[uuid]
        if not uuid or not sinfo {
            return false
        }
        sinfo[hkey] = hval
        _updateTime(Self._lst, sinfo.tm)
        return true
    }

    -- check in session first, clear session hkey, hval table
    static fn clearSession(req, skey) {
        if not req or not skey {
            return false
        }
        uuid = req.cookies[skey]
        sinfo = Self._sessions[uuid]
        if uuid and sinfo {
            Self._lst:remove(sinfo.tm)
            Self._sessions[uuid] = nil
        }
        return true
    }

    -- clear outdate session
    static fn clearOutdate(seconds) {
        now = os.time()
        repeat {
            tm = Self._lst:first()
            if not tm or now - tm.time < seconds {
                break
            }
            Self._lst:popf(tm)
            Self._sessions[tm.uuid] = nil
        } until tm == nil
    }
}

return Session
